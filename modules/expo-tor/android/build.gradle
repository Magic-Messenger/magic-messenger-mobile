apply plugin: 'com.android.library'

group = 'expo.modules.tor'
version = '0.7.6'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 36)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

repositories {
  maven { url "https://raw.githubusercontent.com/guardianproject/gpmaven/master" }
}

dependencies {
  implementation 'info.guardianproject:jtorctl:0.4.5.7'
  implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
}

android {
  namespace "expo.modules.tor"
  defaultConfig {
    versionCode 1
    versionName "0.7.6"
    buildConfigField 'String', 'VERSION_NAME', "\"${versionName}\""
  }
  
  buildFeatures {
    buildConfig = true
  }
  
  lintOptions {
    abortOnError false
  }
  
  sourceSets {
    main {
      jniLibs.srcDirs = ['src/main/jniLibs']
    }
  }
}
